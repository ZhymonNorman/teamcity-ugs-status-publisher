import org.jetbrains.kotlin.gradle.dsl.JvmTarget

plugins {
    id 'java'
    id 'io.github.rodm.teamcity-server' version '1.5'
    id 'io.github.rodm.teamcity-environments' version '1.5'
    id 'org.jetbrains.kotlin.jvm' version '1.9.0'
}

group = 'no.tjer.teamcity'

ext {
    teamcityVersion = '2023.05'

    downloadsDir = project.findProperty('downloads.dir') ?: "$rootDir/downloads"
    serversDir = project.findProperty('servers.dir') ?: "$rootDir/servers"
    java11Home = project.findProperty('java11.home') ?: '/opt/jdk-11.0.2'
}

dependencies {
    compileOnly files("$rootDir/dependencies/commit-status-publisher-${rootProject.ext.teamcityVersion}.jar", "$rootDir/dependencies/server-${rootProject.ext.teamcityVersion}.jar")

    implementation 'org.apache.httpcomponents:httpclient:4.5.1'
    implementation 'org.json:json:20230227'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
}

teamcity {
    version = teamcityVersion

    server {
        descriptor {
            name = project.name
            displayName = "Unreal Game Sync Status Publisher"
            version = project.version
            vendorName = 'Simon Norman'
            vendorUrl = 'https://github.com/ZhymonNorman/teamcity-ugs-status-publisher'
            description = 'Publish build status to Unreal Game Sync (UGS). Forked from Jørgen Tjernø'
            downloadUrl = 'https://github.com/ZhymonNorman/teamcity-ugs-status-publisher/releases/latest/'
            email = ''
            useSeparateClassloader = true
            allowRuntimeReload = true

            dependencies {
                plugin('commit-status-publisher')
            }
        }
    }

    environments {
        downloadsDir = project.downloadsDir
        baseHomeDir = project.serversDir
        baseDataDir = 'data'

        'teamcity2023.05' {
            version = '2023.05'
            javaHome = file(java11Home)
            serverOptions '-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=*:5005'
            agentOptions '-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=*:5006'
        }
    }
}

repositories {
    mavenCentral()
}

compileKotlin {
    compilerOptions {
        jvmTarget = JvmTarget.JVM_18
    }
}

compileTestKotlin {
    compilerOptions {
        jvmTarget = JvmTarget.JVM_11
    }
}
